Clear 
Set DefaultBaseFrequency=60

new circuit.IEEE13Nodeckt 
~ basekv=4.16 pu=1.0001 phases=3 bus1=SourceBus  
~ Angle=0.0 ! advance angle 30 deg so result agree with published angle
~ MVAsc3=20000 MVASC1=21000 ! stiffen the source to approximate inf source
~ baseMVA=1

!LINE CODES
redirect IEEELineCodes.dss

// these are local matrix line codes
// corrected 9-14-2011
New linecode.mtx601 nphases=3 BaseFreq=60 
~ rmatrix = (0.3465 | 0.1560 0.3375 | 0.1580 0.1535 0.3414 ) 
~ xmatrix = (1.0179 | 0.5017 1.0478 | 0.4236 0.3849 1.0348 ) 
~ units=mi 
New linecode.mtx602 nphases=3 BaseFreq=60 
~ rmatrix = (0.7526 | 0.1580 0.7475 | 0.1560 0.1535 0.7436 ) 
~ xmatrix = (1.1814 | 0.4236 1.1983 | 0.5017 0.3849 1.2112 ) 
~ units=mi 
New linecode.mtx603 nphases=2 BaseFreq=60 
~ rmatrix = (1.3238 | 0.2066 1.3294 ) 
~ xmatrix = (1.3569 | 0.4591 1.3471 ) 
~ units=mi 
New linecode.mtx604 nphases=2 BaseFreq=60 
~ rmatrix = (1.3238 | 0.2066 1.3294 ) 
~ xmatrix = (1.3569 | 0.4591 1.3471 ) 
~ units=mi 
New linecode.mtx605 nphases=1 BaseFreq=60 
~ rmatrix = (1.3292 ) 
~ xmatrix = (1.3475 ) 
~ units=mi 
New Linecode.mtx606 nphases=3 BaseFreq=60
~ rmatrix=[0.791721  |0.318476  0.781649  |0.28345  0.318476  0.791721  ]
~ xmatrix=[0.438352  |0.0276838  0.396697  |-0.0184204  0.0276838  0.438352  ]
!~ cmatrix=[383.948  |0  383.948  |0  0  383.948  ]
~ units=mi
New linecode.mtx607 nphases=1 BaseFreq=60 
~ rmatrix = (1.3425 ) 
~ xmatrix = (0.5124 )
!~ cmatrix = [236] 
~ units=mi 


!LOAD DEFINITIONS 
New Load.671 Bus1=671.1.2.3  Phases=3 Conn=Wye Model=1 kV=2.40177711983   kW=1155 kvar=660 
New Load.634a Bus1=634.1     Phases=1 Conn=Wye  Model=1 kV=2.40177711983  kW=160   kvar=110 
New Load.634b Bus1=634.2     Phases=1 Conn=Wye  Model=1 kV=2.40177711983  kW=120   kvar=90 
New Load.634c Bus1=634.3     Phases=1 Conn=Wye  Model=1 kV=2.40177711983  kW=120   kvar=90 
New Load.645 Bus1=645.2       Phases=1 Conn=Wye  Model=1 kV=2.40177711983      kW=170   kvar=125 
New Load.646 Bus1=646.2.3    Phases=2 Conn=Wye Model=1 kV=2.40177711983    kW=230   kvar=132 
New Load.692 Bus1=692.3.1    Phases=2 Conn=Wye Model=1 kV=2.40177711983    kW=170   kvar=151 
New Load.675a Bus1=675.1    Phases=1 Conn=Wye  Model=1 kV=2.40177711983  kW=485   kvar=190 
New Load.675b Bus1=675.2    Phases=1 Conn=Wye  Model=1 kV=2.40177711983  kW=68   kvar=60 
New Load.675c Bus1=675.3    Phases=1 Conn=Wye  Model=1 kV=2.40177711983  kW=290   kvar=212 
New Load.611 Bus1=611.3      Phases=1 Conn=Wye  Model=1 kV=2.40177711983  kW=170   kvar=80 
New Load.652 Bus1=652.1      Phases=1 Conn=Wye  Model=1 kV=2.40177711983  kW=128   kvar=86 
New Load.670a Bus1=670.1    Phases=1 Conn=Wye  Model=1 kV=2.40177711983  kW=17    kvar=10 
New Load.670b Bus1=670.2    Phases=1 Conn=Wye  Model=1 kV=2.40177711983  kW=66    kvar=38 
New Load.670c Bus1=670.3    Phases=1 Conn=Wye  Model=1 kV=2.40177711983  kW=117  kvar=68 

!CAPACITOR DEFINITIONS
New Capacitor.Cap1 Bus1=675.1.2.3 phases=3 kVAR=600 kV=2.40177711983 
New Capacitor.Cap2 Bus1=611.3 phases=1 kVAR=100 kV=2.40177711983

!LINE DEFINITIONS
New Line.sbs_650    Phases=3 Bus1=sourcebus.1.2.3   Bus2=650.1.2.3  LineCode=mtx601 Length=100 units=ft
New Line.650_632    Phases=3 Bus1=650.1.2.3   Bus2=632.1.2.3  LineCode=mtx601 Length=2000 units=ft
New Line.632_670    Phases=3 Bus1=632.1.2.3    Bus2=670.1.2.3  LineCode=mtx601 Length=667  units=ft
New Line.670_671    Phases=3 Bus1=670.1.2.3    Bus2=671.1.2.3  LineCode=mtx601 Length=1333 units=ft
New Line.671_680    Phases=3 Bus1=671.1.2.3    Bus2=680.1.2.3  LineCode=mtx601 Length=1000 units=ft
New Line.632_633    Phases=3 Bus1=632.1.2.3    Bus2=633.1.2.3  LineCode=mtx602 Length=500  units=ft
New Line.633_634    Phases=3 Bus1=633.1.2.3    Bus2=634.1.2.3  LineCode=mtx602 Length=500  units=ft
New Line.632_645    Phases=2 Bus1=632.3.2      Bus2=645.3.2    LineCode=mtx603 Length=500  units=ft
New Line.645_646    Phases=2 Bus1=645.3.2      Bus2=646.3.2    LineCode=mtx603 Length=300  units=ft
New Line.671_692    Phases=3 Bus1=671.1.2.3    Bus2=692.1.2.3  LineCode=mtx606 Length=500  units=ft
New Line.692_675    Phases=3 Bus1=692.1.2.3    Bus2=675.1.2.3  LineCode=mtx606 Length=500  units=ft
New Line.671_684    Phases=2 Bus1=671.1.3      Bus2=684.1.3    LineCode=mtx604 Length=300  units=ft
New Line.684_611    Phases=1 Bus1=684.3        Bus2=611.3      LineCode=mtx605 Length=300  units=ft
New Line.684_652    Phases=1 Bus1=684.1        Bus2=652.1      LineCode=mtx607 Length=800  units=ft


Set Voltagebases=[4.16]
calcv
Solve
BusCoords IEEE13Node_BusXY.csv

!---------------------------------------------------------------------------------------------------------------------------------------------------
!----------------Show some Results -----------------------------------------------------------------------------------------------------------------
!---------------------------------------------------------------------------------------------------------------------------------------------------


// Show Voltages LN Nodes
// Show Currents Elem
// Show Powers kVA Elem
// Show Losses
// Show Taps

!---------------------------------------------------------------------------------------------------------------------------------------------------
!---------------------------------------------------------------------------------------------------------------------------------------------------
! Alternate Solution Script
! To force the taps to be same as published results, set the transformer taps manually and disable the controls
!---------------------------------------------------------------------------------------------------------------------------------------------------
/*
Transformer.Reg1.Taps=[1.0 1.0625]
Transformer.Reg2.Taps=[1.0 1.0500]
Transformer.Reg3.Taps=[1.0 1.06875]
Set Controlmode=OFF

Solve
*/